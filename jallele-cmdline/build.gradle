dependencies {
    implementation project(':jallele-engine')
    implementation 'args4j:args4j:2.33'
    implementation 'junit:junit:4.+'
    implementation 'org.testng:testng:6.+'
}

def attr = [
        'Premain-Class' : 'com.github.gliptak.jallele.Agent',
        'Agent-Class' : 'com.github.gliptak.jallele.Agent',
        'Can-Redefine-Classes' : true,
        'Can-Retransform-Classes' : true,
        'Main-Class' : 'com.github.gliptak.jallele.Main'
        ]

test {
    jvmArgs '-Djdk.attach.allowAttachSelf=true'
}

task uberjar(type: Jar) {
	manifest {
		attributes attr
	}
	archiveBaseName = project.name + '-all'
	from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

task uberjartest(type: Jar) {
    manifest {
        attributes attr
    }
    archiveBaseName = project.name + '-test'
    from files(sourceSets.test.output)
    from { configurations.testCompile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

artifacts {
    archives uberjar
    archives uberjartest
}

jar {
    manifest {
        attributes attr
    }
}
